@echo off
rem
rem $Id: BuildLib.bat,v 1.7 2015-03-10 01:58:13 fyurisich Exp $
rem
rem Build ooHG libraries from official distro
rem
rem Use: BuildLib [f] [hbmk2 options]
rem f parameter redirectes all output to a file named error.log
rem
rem hbmk2 usefull options:
rem -rebuild to force compiling of all sources
rem -trace to see the commands generated by hbmk2
cls

rem *** Set Paths ***
if "%HG_ROOT%"==""  set HG_ROOT=c:\oohg
if "%HG_HRB%"==""   set HG_HRB=c:\oohg\harbour
if "%HG_MINGW%"=="" set HG_MINGW=c:\oohg\mingw

rem *** To Build with Nightly Harbour ***
rem set HG_HRB=c:\hb32
rem *** For 32 bits MinGW ***
rem set HG_MINGW=%HG_HRB%\comp\mingw
rem *** For 64 bits MinGW ***
rem set HG_MINGW=%HG_HRB%\comp\mingw64

rem *** Set EnvVars ***
if "%LIB_GUI%"=="" set LIB_GUI=lib
if "%BIN_HRB%"=="" set BIN_HRB=bin

rem *** To Build with Nightly Harbour ***
rem *** For 32 bits MinGW ***
rem set LIB_GUI=lib\hb\mingw
rem set BIN_HRB=bin or bin\win\mingw
rem *** For 64 bits MinGW ***
rem set LIB_GUI=lib\hb\mingw64
rem set BIN_HRB=bin or bin\win\mingw64

rem *** Set PATH ***
set TPATH=%PATH%
set PATH=%HG_MINGW%\bin;%HG_HRB%\%BIN_HRB%

rem *** Delete Old Log ***
if exist error.log del error.log

rem *** Check for Log Messages Switch ***
if .%1.==.f. goto FILE
if .%1.==.F. goto FILE


:SCREEN
rem *** Build Libraries ***
hbmk2 oohg.hbp      %1 %2 %3 %4 %5 %6 %7 %8 %9
hbmk2 miniprint.hbp %1 %2 %3 %4 %5 %6 %7 %8 %9
hbmk2 hbprinter.hbp %1 %2 %3 %4 %5 %6 %7 %8 %9
goto EXIT


:FILE
rem *** Build Libraries ***
hbmk2 oohg.hbp      %2 %3 %4 %5 %6 %7 %8 %9 >> error.log 2>&1
hbmk2 miniprint.hbp %2 %3 %4 %5 %6 %7 %8 %9 >> error.log 2>&1
hbmk2 hbprinter.hbp %2 %3 %4 %5 %6 %7 %8 %9 >> error.log 2>&1
goto EXIT


:EXIT
rem *** Restore PATH ***
set PATH=%TPATH%
set TPATH=
